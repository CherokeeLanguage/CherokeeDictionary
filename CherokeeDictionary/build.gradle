//CherokeeDictionary (GWT Fontend)

apply plugin: 'java'
apply plugin: 'gwt'
apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'
apply plugin: 'eclipse'
apply plugin: 'eclipse-wtp'

sourceCompatibility = 1.7
compileJava.options.encoding = 'UTF-8'

version = '1.0'

farm {
  webapp ':'
}

def RTJAR = '/usr/lib/jvm/java-7-openjdk-amd64/jre/lib/rt.jar';
if (new File(RTJAR).exists()) {
	compileJava.options.bootClasspath=RTJAR
}

buildscript {
	repositories {
        jcenter()
    }
    dependencies {
        classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
        classpath 'org.akhikhl.gretty:gretty:+'
    }
}

war {
	archiveName 'CherokeeDictionary.war'
}


gretty {
	port = 8080;
	servletContainer = 'tomcat7'
	managedClassReload = true
	extraResourceBase 'build/gwt/out'
}

repositories {
    jcenter()
	mavenCentral()
	mavenLocal()
}

gwt {
	logLevel = 'INFO'

	minHeapSize = "2048M";
	maxHeapSize = "2048M";

	gwtVersion='2.7.0'

	modules 'com.cherokeelessons.dict.DictModule'
	
	jsInteropMode = "JS"
	
	compiler {
		disableAggressiveOptimization = true;
		disableCastChecking = false;		
		disableClassMetadata = false;
		enableClosureCompiler = false;
		enforceStrictResources = false;
		overlappingSourceWarnings = false;
		strict = true;
		style = "OBF";
	}
	
	superDev {
		noPrecompile = false;
		failOnError = false;
	}
	
	eclipse {
		addGwtContainer = true;
	}
}


configurations {
  provided
}

dependencies {
	providedCompile 'javax.servlet:javax.servlet-api:3.0.1'
	providedCompile 'javax.ws.rs:javax.ws.rs-api:2.0.1'
	providedCompile 'com.google.gwt:gwt-servlet:2.7.0'
	gwt 'org.fusesource.restygwt:restygwt:2.0.3'
	gwt 'org.gwtbootstrap3:gwtbootstrap3:0.9.1'
	gwt ('com.google.gwt.eventbinder:eventbinder:1.1.0') {
		exclude module: 'gwt-user'
	}
}
 
sourceSets {
  main {
    compileClasspath += configurations.provided
  }
  test {
    compileClasspath += configurations.provided
    runtimeClasspath += configurations.provided
  }
}

task "create-dirs" << {
	sourceSets*.java.srcDirs*.each { it.mkdirs() }
	sourceSets*.resources.srcDirs*.each { it.mkdirs() }
}

eclipseJdt << {
    File f = file('.settings/org.eclipse.core.resources.prefs')
    f.write('eclipse.preferences.version=1\n')
    f.append('encoding/<project>=utf-8')
    f = file('.settings/org.eclipse.core.runtime.prefs')
    f.write('eclipse.preferences.version=1\n')
    f.append('line.separator=\\n\n')
}

eclipse {
	project {
		name = 'CherokeeDictionary'
		comment = 'Cherokee Dictionary GWT based font-end'
		//referencedProjects 'ResearcherInstituteApi', 'ResearcherInstituteDao', 'ResearcherInstituteShared', 'SHARED-POJOS', 'SHARED-SERVER' 
		natures 'org.springsource.ide.eclipse.gradle.core.nature'
		webAppDirName = 'src/main/webapp'
	}

	//classpath {
	//	plusConfigurations += [configurations.provided]
	//}

	jdt {
		sourceCompatibility=1.7
		targetCompatibility=1.7
	}

	wtp {
		facet {
			facets = []
			facet name: 'jst.java', version: '1.7'
			facet name: 'jst.web', version: '3.0'
		}
	}
}

//ENFORCE JAVA VERSION IN ECLIPSE TO MATCH VERSION SET IN GRADLE
project.afterEvaluate {
	// use jre lib matching version used by project, not the workspace default
	if (sourceCompatibility != null) {
		def target = project.sourceCompatibility.toString()
		def containerPrefix = "org.eclipse.jdt.launching.JRE_CONTAINER"
		def containerSuffix
		if (target =~ /1.[4-5]/) {
			containerSuffix = '/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/J2SE-' + target
		} else if (target =~ /1.[6-8]/) {
			containerSuffix = '/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-' + target
		}
		if (containerSuffix != null) {
			project.eclipse.classpath {
				containers.removeAll { it.startsWith(containerPrefix) }
				containers.add(containerPrefix + containerSuffix)
			}
		}
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}